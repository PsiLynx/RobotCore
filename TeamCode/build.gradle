//
// build.gradle in TeamCode
//
// Most of the definitions for building your module reside in a common, shared
// integrate updates to the FTC into your code. If you really need to customize
// the build definitions, you can place those customizations in this file, but
// please think carefully as to whether such customizations are really necessary
// before doing so.


// Custom definitions may go here
buildscript {
    repositories {
        google()
        mavenCentral()
        gradlePluginPortal()
        maven { url 'https://www.matthewo.tech/maven/' }
        maven { url 'https://repo.dairy.foundation/releases' }
        maven { url 'https://repo.dairy.foundation/private' }
    }
    dependencies {
        //classpath "dev.frozenmilk:Load:0.1.0"
        //classpath 'org.psilynx:psikit:0.0.1'
        classpath 'com.peterabeles.gversion:com.peterabeles.gversion.gradle.plugin:1.10.3'
    }
}


apply from: '../build.common.gradle'
apply from: '../build.dependencies.gradle'

apply plugin: 'kotlin-android'
apply plugin: 'com.peterabeles.gversion'
//apply plugin: 'org.psilynx.psikit'
//apply plugin: 'dev.frozenmilk.sinister.sloth.load'
gversion {
    srcDir       = "src/main/java/"
    // path
    // is relative to the
    // sub-project by default
    // Gradle variables can also be used
    // E.g. "${project.rootDir}/module/src/main/java"
    classPackage = "org.firstinspires.ftc.teamcode.util.gversion"
    className    = "GVersion"                // optional. If not specified GVersion
    dateFormat   = "yyyy-MM-dd'T'HH:mm:ss'Z'" // optional. This is the default
    timeZone     = "UTC"                      // optional. UTC is default
    debug        = false                      // optional. print out extra debug information
    language     = "kotlin" // optional. Can be Java, Kotlin, YAML, or
    // Properties. Case insensitive.
    explicitType = true                      // optional. Force types to be explicitly printed
    indent       = "\t"                       // optional. Change how code is indented. 1 tab is default.
}
task eventDeploy {
    doLast {
        def branchPrefix = "event"
        def branch = 'git branch --show-current'.execute().text.trim()
        def commitMessage = "Update at '${new Date().toString()}'"

        if (branch.startsWith(branchPrefix)) {
            println "Adding all changes..."
            def add = ['git', 'add', '-A'].execute(null, projectDir)
            add.in.eachLine { println it }
            add.err.eachLine { System.err.println it }
            add.waitFor()

            println "Committing changes..."
            def commit = ['git', 'commit', '-m', commitMessage].execute(null, projectDir)
            commit.in.eachLine { println it }
            commit.err.eachLine { System.err.println it }
            commit.waitFor()

            println "Committed to branch: '$branch'"
            println "Commit message: '$commitMessage'"
        } else {
            println "Not on an event branch, skipping commit"
        }
    }
}

android {
    namespace = 'org.firstinspires.ftc.teamcode'
    androidResources {
        noCompress 'tflite'
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    packagingOptions {
        jniLibs {
            pickFirsts += ['**/*.so']
        }
        jniLibs.useLegacyPackaging true
    }

    testOptions{
        unitTests{
            includeAndroidResources = true
        }
    }
}
repositories {
    google()
    mavenCentral()
    //maven {url 'https://repo.dairy.foundation/releases' }
    maven {
        name = "dairyPrivate"
        url = uri('https://repo.dairy.foundation/private')
        credentials(PasswordCredentials)
        authentication {
            basic(BasicAuthentication)
        }
    }
}

dependencies {
    implementation project(':FtcRobotController')

    implementation 'androidx.core:core-ktx:1.13.1'

    testImplementation "org.robolectric:robolectric:4.12.1"
    testImplementation 'junit:junit:4.12'
    testImplementation "io.mockk:mockk:1.14.5"
    testImplementation "io.mockk:mockk-android:1.14.5"
    testImplementation "io.mockk:mockk-agent:1.14.5"


    androidTestImplementation 'junit:junit:4.12'


    annotationProcessor files('lib/OpModeAnnotationProcessor.jar')
    implementation "org.jetbrains.kotlin:kotlin-reflect:2.0.0"

    implementation 'com.fasterxml.jackson.core:jackson-databind:2.12.7'
    implementation 'org.jetbrains.kotlin:kotlin-stdlib:2.0.0'

    implementation 'org.psilynx.psikit:ftc:0.1.0-alpha3-1'
    implementation 'org.psilynx.psikit:core:0.1.0-alpha3-1'


    implementation 'com.acmerobotics.dashboard:dashboard:0.4.17'

    //implementation 'dev.frozenmilk.sinister:Sloth:0.1.0'

    implementation 'org.openftc:easyopencv:1.5.0' // Adjust the version as necessary

}
